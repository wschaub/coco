 ORG $4000
*string to open HELLO/DAT for reading
*as file 1
OPENS FCB $99
      FCN '"I",#1,"HELLO/DAT"'
      FCB 0
*string to open WRITE/DAT for writing
*as file 2
OPENWS FCB $99
       FCN '"O",#2,"WRITE/DAT"'
       FCB 0
*close file 1
CLOSES FCB $9A
       FCN '#1'
       FCB 0
*close file 2
CLOSEWS FCB $9A
        FCN '#2'
        FCB 0
*string to write to file 2
WRITES FCN 'HELLO WORLD!'
       FCB 0
DEVNUM EQU $6F  ;address of current console file/device number
CONIN  EQU $016A ; vector address for console in
CONOUT EQU $0167 ; vector address for console out
*console in buffer status address
* 00 = not empty
* FF = empty/end of file
CINBFL EQU $0070 
Start:
    ;open a file using BASIC OPEN call from BASIC, yes this is chating..
    LDX <$A6
    PSHS X ;save program pointer
    LEAX OPENS,PCR
    BSR BASIC ; open file 1 for reading
    LDY #$400
loop LDA #$1 ;read first char of file 1
    STA <DEVNUM
    LDX #CONIN
    JSR DOS
    LDB CINBFL ;load console in buffer flag
    CMPB #$FF
    BEQ write
    CMPY #$600
    BEQ wrap
    STA ,Y+
    BRA loop
wrap LDY #$400
     STA ,Y+
     JMP loop
write    LEAX CLOSES,PCR
    BSR BASIC
    LEAX OPENWS,PCR
    BSR BASIC
    LDA #$2
    STA <DEVNUM
    LEAY WRITES,PCR
writeloop   LDA ,Y+
            CMPA $0
            BEQ _writeend
            LDX #CONOUT
            BSR DOS
            BRA writeloop
_writeend:
            LEAX CLOSEWS,PCR 
            BSR BASIC
rtb: 
    PULS X ; restore settings to return to basic on RTS
    CLR <DEVNUM
    STX <$A6
    RTS
BASIC   STX <$A6
        LDA ,X
        ANDCC #$FE
        JSR $ADC6
        RTS
;JSR to the subroutine at vector X
;skip over the opcode at the vector using indirect addressing.
;all vector hooks will call LEAS $2,S so we push our return address
;to the stack before calling. This seems to prevent our return
;address getting clobbered. 
DOS     PSHS Y ;preserve Y
        LDY  #_DOSEND ;grab address to return to
        PSHS Y        ;push it to the stack so we can return
        JSR [1,X] ;jump to vector pointed to in X skip over the JMP instruction stored there.
_DOSEND    PULS Y,PC ;control should return here
    END Start
